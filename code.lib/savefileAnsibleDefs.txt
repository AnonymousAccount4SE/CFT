# Readme
<<<<<< EOF
Parsing "Ansible" text format looking like this:

# This is a comment

[Auto.reboot=yes]
*.v 
s*.s   [no]



>>>>>> EOF
/Readme

# Parse text lines. Returns dictionary with 'data' pointing at the
# actual data structure, and then the following lambdas, which are
# used as follows:
#
#    LAllElements.call
#    LAllFields.call
#    LGet.call(elementName,fieldName,defaultValue?)
#    LDump.call
#
# The data dictionary has keys for all elements, and under each
# is a Dict with the settings for that element.
# --
P(1,List) => lines
	data = Dict
		
	currProp=null
	currPropValue=null

	LFilter = Lambda{
		P(1,List) => list
		P(2) => regex
		list->s assert(regex.match(s)) out(s)
	}
	LApply = Lambda {
		P(1) => data
		P(2) => entities
		P(3) => name
		P(4) => value

		entities->e
			#println(e + "." + name + "=" + value)
			data.get(e,Dict).set(name,value)
	}

	lines->line
		reject(line.trim.startsWith("#"))
		reject(line.trim=="")
		if (line.trim.startsWith("[")) {
			# property definition with default value
			line.after("[").before("]").split("=").push(2) => currProp =>currPropValue 
		} else {
			# data line with optional exception
			line.before("[").trim => entity
			if (entity.contains("*")) {
				regex=Glob(entity,false).regex
			
				# keys of data dict are the entities that we filter
				entities=LFilter.call(data.keys,regex)
			} else {
				entities=List(entity)
			}

			# is there an exception?
			redef=line.after("[").before("]").trim
			if(redef != "") {
				LApply.call(data, entities,currProp, redef)
			} else {
				LApply.call(data, entities,currProp, currPropValue)
			}
										
		}
	|

	# Create return value with lambdas for analyzing the data
	# --
	Dict
		.set("data",data)
		.set("LAllElements",Lambda{
			self.data.keys
		})
		.set("LAllFields", Lambda{
			self.data.keys->key 
				self.data.get(key).keys->field
					out (field)
			| _.unique
		})
		.set("LGet", Lambda{
			P(1)=>elementName
			P(2)=>field
			P(3)=>defaultValue

			result=defaultValue

			if (self.data.has(elementName)) {
				e=self.data.get(elementName)
				if (e.has(field)) {
					x=e.get(field)
					if (x != null) result=x
				}
			}

			result
		})
		.set("LDump",Lambda{
			self.data.keys->element
				self.data.get(element).keys->field
					value=self.data.get(element).get(field)
					out(List(element,field,value))
		})
/Parse



Lib:DirPrivate.file("Hosts.txt").read
/lines

Parse(lines)
/t
