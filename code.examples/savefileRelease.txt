# License
<<< EOF
#
# CFT - an interactive programmable shell for automation 
# Copyright (C) 2020 Roar Foshaug
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, version 3 of the License.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>
#
>>> EOF
/License



# Readme for script
<<< EOF
---------------------------------------------------
This script manages git both locally and remotely,
and contains three major functions:

Add, Commit and Push.
---------------------------------------------------
>>> EOF
/Readme




# Project home directory
	Sys.savefile.dir.sub("..")
/HomeDir




# Move save files from CFT home dir (which is the work dir) then run GIT add before restoring  files
# Lib and example files are stored in code.lib and code.examples
# ---

	# Update the Overview.txt file
	call "Overview:CreateOverview"
	call "MD:Run"

	# Identify files to move temporarily out of the way, so as not to be included in git add .
	HomeDir.files(Glob("savefile*"))  
	+ HomeDir.file(".editHistory")
	+ HomeDir.file("projects.txt")
	=FilesToMove

	# Create temp-dir
    Dir("/tmp/release"+currentTimeMillis) =d
    when (!d.exists, d.create)
    d =TempDir

	# Move files to TempDir
    FilesToMove->f
	assert(f.exists)
        println("Copying file " + f.name + " to " + TempDir.path)
        when (!TempDir.copy(f), error("Failed to copy file"))
        println("Deleting file " + f.name)
        f.delete
    |

	# Run git
    HomeDir.run("git","add",".")
    GitStatus

	# Restore files
    TempDir.files->f
        println("Restoring file " + f.name)
        when (!HomeDir.copy(f), error("Failed to restore file from " + TempDir.path))
        f.delete
    |

	# Delete temp-dir (should be empty)
	println("Deleting temp dir " + d.path)
	d.delete
/Add



# Commit changes
	GitStatus
	call "Lib:ReadValue" ("Commit message?", Sys.version) =msg
	HomeDir.run("git","commit","-m",msg)
/Commit


# Push latest commits
	HomeDir.run("git","push","origin","master")
/Push





# Git status
	HomeDir.run("git","status")
/GitStatus




# Git diff
	HomeDir.run("git","diff")
/diff



GitStatus
/gs

diff
/gd


