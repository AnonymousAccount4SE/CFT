
call "Lib:e" (savefile)
/e

# Run command string (must split correctly)
P(1,"dir")=cmd
	println("---> " + cmd)
	Dir.run(cmd.split)
/Run

# List docker containers
Run("docker container list")
/CList

# List docker images
Run("docker image list")
/IList


# Download elasticsearch image
Run("docker pull docker.elastic.co/elasticsearch/elasticsearch:7.8.0")
/esPull

# Download kibana image
Run("docker pull docker.elastic.co/kibana/kibana:7.8.0")
/kibPull

# Download metricbeat image
Run("docker pull docker.elastic.co/beats/metricbeat:7.8.0")
/mbPull


# --- Util: get id for Docker container
P(1,"docker.elastic")=name
Dir.runCapture("docker container list".split) =data
null =result
data->line
	assert(line.contains(name))
	line.split.nth =result
	break(result != null)
|
result
/getId

# --- Util: get IP of local machine
"Ethernet 4" =adaptorName
false =enabled
null =result
Dir.runCapture("ipconfig")->line
	when(line.contains(adaptorName), {true =enabled})
	assert(enabled)
	when(line.contains("IPv4 Address"), {
		line.split(":").nth(1).trim =result
	})
	break(result != null)
|
result
/getLocalIp



# Start elasticsearch container
Run('docker run -p 9200:9200 -p 9300:9300 -e "discovery.type=single-node" docker.elastic.co/elasticsearch/elasticsearch:7.8.0')
/esRun


# Start kibana container
Run('docker run --link ' + getId("elasticsearch") + ':elasticsearch -p 5601:5601 docker.elastic.co/kibana/kibana:7.8.0')  # {docker-repo}:{version}
/kibRun


# Start metricbeat container
getLocalIp =localIp
Run('docker run docker.elastic.co/beats/metricbeat:7.8.0 setup -E setup.kibana.host='+localIp+':5601 -E output.elasticsearch.hosts=["'+localIp+':9200"]')
/mbRun


# Stop and remove elasticsearch container
getId("elasticsearch") =id
Run("docker stop " + id)
Run("docker rm " + id)
/esRemove


# Stop and remove kibana container
getId("kibana") =id
Run("docker stop " + id)
Run("docker rm " + id)
/kibRemove


# Stop and remove metricbeat container
getId("metricbeat") =id
Run("docker stop " + id)
Run("docker rm " + id)
/mbRemove



