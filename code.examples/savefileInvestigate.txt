# License
<<< EOF
#
# CFT - an interactive programmable shell for automation 
# Copyright (C) 2020 Roar Foshaug
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, version 3 of the License.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>
#
>>> EOF
/License

# Readme
<<< EOF
-------------------------------------------------------------------
Creating an investigation log when working with finding out why
a system behaves as it does. Uses Sys.lastResult to obtain
data.

To start new log or save existing, just open the LogFile
in an editor.
-------------------------------------------------------------------
>>> EOF
/Readme


File(".investigate")
/LogFile




# Log with data
	List("","D:"+Date.fmt,"----") =list

	Sys.lastResult =res
	res.?nth =isList

	when(isList,{
		readLine("Element number (Enter for all)") =num
		when(num.?parseInt, {res.nth(num.parseInt) =res})
	})
	
	0 =count
	res->x 
		list.add("# " +x) 
		count+1 =count
	|
	list.add("----")

	loop
		readLine("Comment, blank when done") =line
		break(line=="")
		list.add("C:" + line)
	|
	list.add("")
	
	LogFile.append(list)
/LogWithData


# Log multiple lines, no data
	List("","D:"+Date.fmt) =list

	loop
		readLine("Comment, blank when done") =line
		break(line=="")
		list.add("C:" + line)
	|
	list.add("")
	LogFile.append(list)
/LogNoData


# Log pasted text
	List("","D:"+Date.fmt,"----") =list

	readLine("End marker") =endMarker
	readLines(endMarker) =lines
	
	lines->line list.add("# " + line) |
	
	list.add("----")
	loop
		readLine("Comment, blank when done") =line
		break(line=="")
		list.add("C:" + line)
	|
	
	list.add("")
	LogFile.append(list)
/LogPastedText



LogFile.read
/Show

