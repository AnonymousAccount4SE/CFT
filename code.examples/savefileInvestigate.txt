# License
<<< EOF
#
# CFT - an interactive programmable shell for automation 
# Copyright (C) 2020 Roar Foshaug
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, version 3 of the License.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>
#
>>> EOF
/License

# Readme
<<< EOF
-------------------------------------------------------------------
Creating an investigation log when working with finding out why
a system behaves as it does. Uses Sys.lastResult to obtain
data.

To start new log or save existing, just open the LogFile
in an editor.
-------------------------------------------------------------------
>>> EOF
/Readme




# file to append investigation data to
    Date.setFormat("yyyy-MM-dd").fmt =today
    Sys.homeDir.sub("investigate.d") =dir
    ?(!dir.exists, dir.create)
    # return value
    dir.file(today+".txt")
/LogFile



# Input mode
loop
    readLine("Enter text, '..' to quit or '..h' for help").trim =line
    false =terminate
    false =done
    ?(line=="",{LogFile.append("")})
    reject(line=="")

    ?(line.startsWith(".."), {
        line.sub(2)=cmd
        ?(cmd.startsWith("h"), { ## help
            println
            println
            println("---")
            println(".xxx              - read pasted input lines, with end-marker 'xxx'")
            println(".                 - read pasted input lines, with end-marker '.'")
            println("..h               - help, this page")
            println("..c               - show log file (cat)")
            println("..m               - show log file (more)")
            println("..e               - edit log file")
            println("..                - quit")
            println("---")
            println
            true =done
        })
        ?(cmd.startsWith("c"),{  ## cat
            {LogFile.read->line println(line)}
            true =done
        })
        ?(cmd.startsWith("m"),{ ## more
            call "Lib:m" (LogFile)
            true =done
        })
        ?(cmd.startsWith("e"),{ ## edit
            Edit
            true =done
        })
        ?(cmd=="",{ ## quit
            true =terminate
        })
    })
    break(terminate)
    assert(!done)
    (line.startsWith(".."))
    ?(line.startsWith("."), {
        line.sub(1)=marker

        # basic sanity checking
        ?(marker.startsWith(".") || marker.length>10,  {
            println("Invalid end marker: '" + marker + "'")
        }, {
            ?(marker=="",".",marker)=marker
            println("End-marker: '" + marker + "'")
            println("---")
            readLines(marker) =lines
            List =list
            list.add("")
            {lines->line list.add("# " + line)}
            list.add("")
            LogFile.append(list)
        })
    },{
        LogFile.append(Date.fmt + " " + line)
    })
/InputMode




# Log data
    Sys.lastResult =res
    res.?nth =isList

    ?(isList,{
        readLine("Element number (Enter for all)") =num
        ?(num.?parseInt, {res.nth(num.parseInt) =res})
    })

    List =list
    list.add("")    
    res->x 
        list.add("# " +x) 
    |
    list.add("")
    LogFile.append(list)
/LogData


# Log text
    List("","D:"+Date.fmt) =list

    readLine("Comment") =line
    LogFile.append(Date.fmt + " " + line)
/LogText


# Log pasted text
    readLine("Type end marker, or just press Enter to use '.'").trim =endMarker
    ?(endMarker=="",".",endMarker) =endMarker
    println("End-marker: '" + endMarker + "'")
    println("---")
    readLines(endMarker) =lines
    
    List =list
    list.add("")
    lines->line list.add("# " + line) |
    list.add("")
    LogFile.append(list)
/LogPastedText



LogFile.read->line println(line)
/Show

call "Lib:e" (LogFile)
/Edit

