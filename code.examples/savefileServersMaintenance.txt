# Readme
<<< EOF
------------------------------------------------
Example script doing automated update on remote
linux (Ubuntu) hosts. Uses apt package manager.

Requirements:

- Ensure ssh without password
- Ensure sudo without password

To do this

$ :load SSH
$Â Readme

Main function of this script is LinuxAptUpdate, 
which requires an ssh target string on the form

	username@host

It can either be provided as a parameter, or 
you will be interactively asked for it.
------------------------------------------------
>>> EOF
/Readme


Input("SSH target on format username@server").get
/ReadSSHTarget


P(1)=target	# Check if server is up
	if(target.contains("@"), target.after("@"), target) =host
	Dir.runCapture("ping","-c","1",host)->line assert(line.contains("0 received")) out(line) | _.length>0 =failure
	failure
/HostDown


# Run single command via ssh command line
P(1)=target P(2)=singleCommand
	error(HostDown(target),"Unknown host: " + target)

	Dir.runCapture("ssh",target,singleCommand)
/RemoteCommand


P(1)=name Dir("/tmp").file(name + currentTimeMillis)
/TmpFile

# Run multi-line command sequence in single ssh session via stdin
	P(1)=target
	P(2,List)=commands
	
	error(HostDown(target),"Unknown host: " + target)

	TmpFile("in") =inFile
	TmpFile("out") =outFile
	TmpFile("err") =errFile
	
	inFile.create(commands)
	println("---> " + target)
	Dir.runProcessWait(inFile,outFile,errFile,"ssh",target)	
	outFile.read->line println("      ##  " + line)
	
	{* P(1)=f when(f.exists, f.delete) } =Del
	
	Del.call(inFile)
	Del.call(outFile)
	Del.call(errFile)
/RemoteCommandSet


# Linux update
P(1,ReadSSHTarget)=target
<<< EOF
echo "------ apt-get update"
sudo apt-get -y update
echo "------ apt-get upgrade"
sudo apt-get -y upgrade
echo "------ apt-get dist-upgrade"
sudo apt-get -y dist-upgrade
exit
>>> EOF
=commands
#
RemoteCommandSet(target,commands)
/LinuxAptUpdate



call "SSH:rsh"
/rsh


